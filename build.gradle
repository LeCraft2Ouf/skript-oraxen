import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'eclipse'
    id 'java'
}

group = 'me.Asleepp'
version = '1.0'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

repositories {
    mavenCentral()

    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }

    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }

    maven {
        name = "Skript"
        url = 'https://repo.skriptlang.org/releases'
    }

    maven {
        name = "Oraxen"
        url = "https://repo.oraxen.com/releases"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT"

    implementation 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly('com.github.SkriptLang:Skript:2.7.1') {
        transitive = false
    }

    compileOnly 'io.th0rgal:oraxen:1.170.0'
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.version]
}

shadowJar {
    archiveClassifier.set('') // remplace -all.jar par .jar
    archiveVersion.set(project.version)
    minimize()

    dependencies {
        include(dependency('org.bstats:bstats-bukkit'))
        include(dependency('org.bstats:bstats-base'))
    }

    relocate 'org.bstats', 'me.asleepp.skriptoraxen.bstats'
}

tasks.build {
    dependsOn shadowJar
}

tasks.jar {
    enabled = false
}
